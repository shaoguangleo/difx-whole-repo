AC_INIT([difxcalc], [11.0.2], [David Gordon at NASA Goddard])

AM_INIT_AUTOMAKE([foreign])

AM_SANITY_CHECK

AC_C_BIGENDIAN( \
	[de421_file=DE421_big_Endian], \
	[de421_file=DE421_little_Endian] \
)

# AC_PROG_F77
AC_LANG(Fortran)
AC_PROG_FC
AC_FC_FREEFORM

## gfortran v10 does not like some of this heritage code
## the following test adds a flag to reduce many "errors"
## to "warnings" but there are still a few left...
# test for gfortran -fallow-argument-mismatch option
AC_LANG_ASSERT(Fortran)
ac_fc_aam_FCFLAGS_save=$FCFLAGS
FCFLAGS="$ac_fc_aam_FCFLAGS_save -fallow-argument-mismatch"
AC_COMPILE_IFELSE([[
      program junk
      end
]], [AC_MSG_NOTICE([FC accepts -fallow-argument-mismatch])],
[FCFLAGS="$ac_fc_aam_FCFLAGS_save";
AC_MSG_WARN([FC does not accept -fallow-argument-mismatch])])
rm -f conftest.err conftest.$ac_objext conftest.$ac_ext

AC_PROG_CC

AC_CHECK_LIB(m, sin,,)
PKG_CHECK_MODULES(GSL, gsl)

LIBS="${LIBS} ${GSL_LIBS}"
CFLAGS="${CFLAGS} ${GSL_CFLAGS}"

AC_SUBST(de421_file)


AC_OUTPUT([
	Makefile \
	data/Makefile \
	src/Makefile \
	src/param11.i \
])
